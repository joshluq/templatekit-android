pluginManagement {
    val repositoryUrl = providers.gradleProperty("repositoryUrl").get()

    fun MavenArtifactRepository.setupGithub(repositoryName: String) {
        url = uri("$repositoryUrl/$repositoryName")
        credentials {
            username = System.getenv("GITHUB_ACTOR") ?: providers.gradleProperty("gpr.user").orNull
            password = System.getenv("GITHUB_TOKEN") ?: providers.gradleProperty("gpr.key").orNull
        }
    }

    repositories {
        maven { setupGithub("pluginkit-android") }
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    val repositoryUrl = providers.gradleProperty("repositoryUrl").get()
    val catalogVersion = providers.gradleProperty("catalogVersion").get()

    fun MavenArtifactRepository.setupGithub(repositoryName: String) {
        url = uri("$repositoryUrl/$repositoryName")
        credentials {
            username = System.getenv("GITHUB_ACTOR") ?: providers.gradleProperty("gpr.user").orNull
            password = System.getenv("GITHUB_TOKEN") ?: providers.gradleProperty("gpr.key").orNull
        }
    }

    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        maven { setupGithub("pluginkit-android") }
        google()
        mavenCentral()
    }
    versionCatalogs {
        create("libs") {
            from(catalogVersion)
        }
    }
}

rootProject.name = "{{ cookiecutter.repo_name }}"
include(":library")
include(":showcase")
project(":library").name = "{{ cookiecutter.library_name | lower | replace(' ', '-') }}"